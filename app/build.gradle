plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.bowoon.android.android_common_library"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        setProperty("archivesBaseName", "${rootProject.name}_v${versionName}_${new Date().format('yyyyMMdd_HH')}")
    }

    signingConfigs {
        releaseWithSignedKey {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseWithSignedKey
        }
    }
    flavorDimensions "version"
    productFlavors {
        demo {
            dimension "version"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
        full {
            dimension "version"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding.enabled = true

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def buildVariant = ""
            if (variant.buildType.name == "release") {
                buildVariant += "RELEASE"
            } else {
                buildVariant += "SNAPSHOT"
            }
            output.outputFileName = "${rootProject.name}_v${variant.versionName}_${new Date().format('yyyyMMdd_HH')}-${buildVariant}.apk"
        }
    }
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.ktx.core_ktx
    implementation deps.appcompat
    implementation deps.material
    implementation deps.constraintlayout
    implementation project(path: ':network')
    implementation project(path: ':common')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation deps.ktx.lifecycle
    kapt deps.ktx.lifecycle_compiler
    implementation deps.ktx.lifecycle_extension
    implementation deps.ktx.viewmodel
    implementation deps.ktx.fragment

    implementation deps.glide.glide
    kapt deps.glide.compiler

    implementation deps.okhttp.okhttp
    implementation deps.okhttp.logging_interceptor

    implementation deps.retrofit.retrofit
    implementation deps.retrofit.retrofit_converter_gson
    implementation deps.retrofit.retrofit_adapter_rxjava

    implementation deps.gson
    implementation deps.rx.java
    implementation deps.rx.android
    implementation deps.rx.kotlin
    implementation deps.rx.binding
}
